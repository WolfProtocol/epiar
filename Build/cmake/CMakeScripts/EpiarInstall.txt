# Epiar Install directives

if(WIN32)
	install(TARGETS EpiarBIN
		DESTINATION .
		COMPONENT "EpiarExe")
	install(DIRECTORY "${Epiar_OUT_DIR}/data"
		DESTINATION .
		COMPONENT "Epiardata")
	install(FILES "${Epiar_OUT_DIR}/Epiar.cmd"
		DESTINATION .
		COMPONENT "Console")
	set(CPACK_GENERATOR NSIS)
	set(CPACK_NSIS_DISPLAY_NAME "Epiar ${EPIAR_VERSION}")
	set(CPACK_NSIS_MUI_ICON "${SCRIPT_DIR}/Installdata/Win32\\\\NSIS_MUI_ICON.ico")
	set(CPACK_NSIS_MUI_UNIICON "${SCRIPT_DIR}/Installdata/Win32\\\\NSIS_MUI_UNIICON.ico")
	set(CPACK_PACKAGE_ICON "${SCRIPT_DIR}/Installdata/Win32\\\\NSIS_BrandImage.bmp")
	set(CPACK_NSIS_WELCOME_BITMAP "${SCRIPT_DIR}/Installdata/Win32\\\\NSIS_Welcome.bmp")
	set(CPACK_NSIS_HELP_LINK "http://www.epiar.net/howtoplay")
	set(CPACK_NSIS_URL_INFO_ABOUT "http://www.epiar.net")
	set(CPACK_NSIS_CONTACT "Maoserr@gmail.com")
	set(CPACK_NSIS_MENU_LINKS
		"EpiarExe.exe" "Epiar"
		"http://www.epiar.net" "Epiar Homepage")
	set(CPACK_PACKAGE_EXECUTABLES "" "")
	set(CPACK_PACKAGE_FILE_NAME
		"Epiar-${EPIAR_VERSION}-${CMAKE_BUILD_TYPE}")
	set(CPACK_TOPLEVEL_DIRECTORY "${CMAKE_BINARY_DIR}/_CPack_Packages/Windows/NSIS")
elseif (UNIX AND NOT APPLE)
	install(TARGETS EpiarBIN
		DESTINATION ./share/Epiar)
	install(DIRECTORY "${Epiar_OUT_DIR}/data"
		DESTINATION ./share/Epiar)
	# Menu link
	install(FILES "${SCRIPT_DIR}/Installdata/Deb/epiar.desktop"
		DESTINATION ./share/applications)
	# Menu icon
	install(FILES "${SCRIPT_DIR}/Installdata/Deb/epiar.png"
		DESTINATION ./share/pixmaps)
	set(CPACK_GENERATOR DEB)
	set(CPACK_DEBIAN_PACKAGE_SECTION "game")
	set(CPACK_DEBIAN_PACKAGE_DESCRIPTION
		"Space combat/trading game.
		Epiar is inspired by the Escape velocity series of games.")
	if(${CMAKE_SYSTEM_PROCESSOR} STREQUAL "x86_64")
		set(CPACK_DEBIAN_PACKAGE_ARCHITECTURE amd64)
	elseif(${CMAKE_SYSTEM_PROCESSOR} STREQUAL "i686")
		set(CPACK_DEBIAN_PACKAGE_ARCHITECTURE i386)
	endif(${CMAKE_SYSTEM_PROCESSOR} STREQUAL "x86_64")
	set( CPACK_DEBIAN_PACKAGE_CONTROL_EXTRA
		${SCRIPT_DIR}/Installdata/Deb/postinst
		${SCRIPT_DIR}/Installdata/Deb/prerm )
	set(CPACK_DEBIAN_PACKAGE_DEPENDS
		"libsdl1.2debian,
		libsdl-image1.2,
		libsdl-mixer1.2,
		libxml2,
		libftgl2")
	set(CPACK_PACKAGE_FILE_NAME
		"Epiar-${EPIAR_VERSION}-Ubuntu-${CPACK_DEBIAN_PACKAGE_ARCHITECTURE}-${CMAKE_BUILD_TYPE}")
elseif (APPLE)
	install(DIRECTORY "${Epiar_OUT_DIR}/data/animations" DESTINATION .)
	install(DIRECTORY "${Epiar_OUT_DIR}/data/audio" DESTINATION .)
	install(DIRECTORY "${Epiar_OUT_DIR}/data/saves" DESTINATION .)
	install(DIRECTORY "${Epiar_OUT_DIR}/data/fonts" DESTINATION .)
	install(DIRECTORY "${Epiar_OUT_DIR}/data/graphics" DESTINATION .)
	install(DIRECTORY "${Epiar_OUT_DIR}/data/icons" DESTINATION .)
	install(DIRECTORY "${Epiar_OUT_DIR}/data/scripts" DESTINATION .)
	set(CPACK_GENERATOR Bundle)
	set(CPACK_PACKAGE_FILE_NAME
		"Epiar-${EPIAR_VERSION}-MacOSX-${CMAKE_SYSTEM_PROCESSOR}-${CMAKE_BUILD_TYPE}")
	set(CPACK_BUNDLE_NAME Epiar)
	set(CPACK_BUNDLE_ICON "${Epiar_OUT_DIR}/data/graphics/Epiar_Icon2.icns")
	set(CPACK_BUNDLE_PLIST "${SCRIPT_DIR}/Installdata/Mac/Info.plist")
	set(CPACK_BUNDLE_STARTUP_COMMAND "${Epiar_OUT_DIR}/Epiar_debug")
	set(CPACK_PACKAGE_ICON "${Epiar_OUT_DIR}/data/graphics/Epiar_Icon2.icns")
endif (WIN32)

set(CPACK_SYSTEM_NAME ${CMAKE_SYSTEM_NAME})
set(CPACK_PACKAGE_NAME "Epiar")
set(CPACK_PACKAGE_VENDOR "Epiar Development Team")
set(CPACK_PACKAGE_CONTACT "Maoserr [maoserr@gmail.com]")
set(CPACK_PACKAGE_VERSION_MAJOR ${EPIAR_VERSION_MAJOR})
set(CPACK_PACKAGE_VERSION_MINOR ${EPIAR_VERSION_MINOR})
set(CPACK_PACKAGE_VERSION_PATCH ${EPIAR_VERSION_PATCH})
set(CPACK_PACKAGE_DESCRIPTION "${Epiar_OUT_DIR}/README")
set(CPACK_PACKAGE_DESCRIPTION_SUMMARY "Epiar is a space combat/trading game.")
set(CPACK_PACKAGE_INSTALL_DIRECTORY "Epiar")
set(CPACK_RESOURCE_FILE_LICENSE "${Epiar_OUT_DIR}/LICENSE")

INCLUDE(CPack)

# Advanced configuration of CPACK
if(WIN32)
	cpack_add_component(EpiarExe
		DISPLAY_NAME "Epiar executable"
		DESCRIPTION "The base Epiar runtime"
		REQUIRED
		GROUP Req
		INSTALL_TYPES Standard Console)
		cpack_add_component(Epiardata
		DISPLAY_NAME "Epiar data"
		DESCRIPTION "Resource files Epiar requires"
		REQUIRED
		GROUP Req
		INSTALL_TYPES Standard Console)
	cpack_add_component(Console
		DISPLAY_NAME "Epiar command line"
		DESCRIPTION "Small batch file for running Epiar on the command line"
		DISABLED
		INSTALL_TYPES Console)
	cpack_add_component_group(Req
		DISPLAY_NAME "Required"
		DESCRIPTION "Required"
		EXPANDED
		BOLD_TITLE)
	cpack_add_install_type(Standard
		DISPLAY_NAME "Standard")
	cpack_add_install_type(Console
		DISPLAY_NAME "With Console")
else(UNIX AND NOT APPLE)

else(APPLE)

endif(WIN32)

# vim:ft=cmake


